apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles fileTree(dir: '../proguard', include: '*.pro').asList().toArray()
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    useLibrary  'org.apache.http.legacy'
}

dependencies {
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.5'
}

group = 'gif'
version = '1.0.0'

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar, dependsOn: generateJavadocs) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = 'Maven'
        name = 'gif'
        userOrg = 'cd'

        version {
            name = '1.0.0-final'
            desc = "a gif for android "
            vcsTag = '1.0.0'
        }

        licenses = ['Apache-2.0']
//        vcsUrl = 'https://github.com/cd89101112/FileSelector.git'
//        websiteUrl = 'https://github.com/cd89101112/FileSelector'
    }

    configurations = ['archives']

}


